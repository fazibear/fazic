use fazic::runtime::ast::{NodeElement, Opcode};
use fazic::runtime::node_builder::*;

#[LALR]
grammar;

match {
    r"(?i)OR" => OR,
    r"(?i)AND" => AND,

    r"(?i)ABS" => ABS,

    r"(?i)PRINT" => PRINT,
    r"(?i)LIST" => LIST,
    r"(?i)REM.*" => REM,
    r"(?i)RUN" => RUN,
    r"(?i)GOTO" => GOTO,

    r"(?i)LET" => LET,
} else {
    r"'[^']*'" => STRING_S,
    r#""[^"]*""# => STRING_D,
    r"[0-9]*\.[0-9]+" => FLOAT,
    r"[0-9]+" => INTEGER,
    r"(?i)[A-Z]+" => VARIABLE,
    _
}

pub all: Vec<NodeElement> = {
    <ast:command> => vec![ast],
    <ast:expression> => vec![ast],
    <ast:command> ":" <rest:all> => {
        let mut cmds = vec![ast];
        cmds.extend(rest);
        cmds

    },
    <ast:expression> ":" <rest:all> => {
        let mut cmds = vec![ast];
        cmds.extend(rest);
        cmds
    },
};

expression: NodeElement = {
    <l:expression> OR <r:prec_1> => param_2_node(Opcode::Or, l, r),
    <l:expression> AND <r:prec_1> => param_2_node(Opcode::And, l, r),
    prec_1
};

prec_1: NodeElement = {
    prec_2
};

prec_2: NodeElement = {
    <l:prec_2> "+" <r:prec_3> => param_2_node(Opcode::Add, l, r),
    <l:prec_2> "-" <r:prec_3> => param_2_node(Opcode::Sub, l, r),
    prec_3,
};

prec_3: NodeElement = {
    <l:prec_3> "*" <r:term> => param_2_node(Opcode::Mul, l, r),
    <l:prec_3> "/" <r:term> => param_2_node(Opcode::Div, l, r),
    "-" <r:term> => param_1_node(Opcode::Neg, r),
    term,
};

term: NodeElement = {
    integer,
    float,
    function,
    string,
    variable,
    "(" <expression> ")",
};

function: NodeElement = {
    ABS "(" <expression> ")" => param_1_node(Opcode::Abs, <>),
};

command: NodeElement = {
    "?" <expression> => param_1_node(Opcode::Print, <>),
    PRINT <expression> => param_1_node(Opcode::Print, <>),
    LIST => param_0_node(Opcode::List),
    REM => param_0_node(Opcode::Rem),
    RUN => param_0_node(Opcode::Run),
    GOTO <integer> => param_1_node(Opcode::Goto, <>),
    LET <variable_name> "=" <expression> => param_2_node(Opcode::Let, <>),
};

string: NodeElement = {
    STRING_S => string_node(<>),
    STRING_D => string_node(<>),
};

float: NodeElement = {
    FLOAT => float_node(<>),
};

integer: NodeElement = {
    INTEGER => integer_node(<>),
};

variable: NodeElement = {
    VARIABLE => variable_node(<>),
};

variable_name: NodeElement = {
    VARIABLE => variable_name(<>),
};
