use fazic::runtime::ast::*;
use fazic::runtime::node_builder::*;

pub parse_all -> Entry
    = i:integer _ a:(all ++ ":") { entry_node(Some(i), a) }
    / a:(all ++ ":") { entry_node(None, a) }

all -> NodeElement
    = _ c:command _ { c }
    / _ e:expression _ { e }

command -> NodeElement
    = ("?" / "PRINT" / "print") _ e:expression { param_1_node(Opcode::Print, e) }

    / ("GOTO" / "goto") _ i:integer { param_1_node(Opcode::Goto, i) }
    / ("GOSUB" / "gosub") _ i:integer { param_1_node(Opcode::Gosub, i) }

    / ("LET" / "let") _ v:varname _ "=" _ e:expression { param_2_node(Opcode::Let, v, e) }

    / ("RETURN" / "return") { param_0_node(Opcode::Return) }
    / ("LIST" / "list")  { param_0_node(Opcode::List) }
    / ("REM" / "rem") .* { param_0_node(Opcode::Rem) }
    / ("RUN" / "run")  { param_0_node(Opcode::Run) }
    / ("END" / "end")  { param_0_node(Opcode::End) }

function -> NodeElement
    = ("ABS(" / "abs(") _ e:expression _ ")" { param_1_node(Opcode::Abs, e) }

expression -> NodeElement
    = #infix<term> {
        #L l ("AND" / "and") r { param_2_node(Opcode::And, l, r) }
           l ("OR" / "or") r { param_2_node(Opcode::Or, l, r) }
        #L l "+" r { param_2_node(Opcode::Add, l, r) }
           l "-" r { param_2_node(Opcode::Sub, l, r) }
        #L l "*" r { param_2_node(Opcode::Mul, l, r) }
           l "/" r { param_2_node(Opcode::Div, l, r) }
    }

term -> NodeElement
    = _ f:float _ { f }
    / _ i:integer _ { i }
    / _ s:string _ { s }
    / _ f:function _ { f }
    / _ "(" _ e:expression _ ")" _ { e }
    / _ "-" _ t:term _ { param_1_node(Opcode::Neg, t) }
    / _ v:variable _ { v }

float -> NodeElement
    = f:$([0-9]* "." [0-9]+) { println!("{}", f); float_node(f) }

integer -> NodeElement
    = i:$([0-9]+) { integer_node(i) }

string -> NodeElement
    = "\"" s:$([^"]*) "\"" { string_node(s) }
    / "'" s:$([^']*) "'" { string_node(s) }

variable -> NodeElement
    = v:$([a-zA-Z]+) { variable_node(v) }

 varname -> NodeElement
     = v:$([a-zA-Z]+) { variable_name(v) }

_ = #quiet<" "*>
